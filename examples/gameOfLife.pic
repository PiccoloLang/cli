#! piccolo

import 'io' as io
import 'time' as time
import 'libs/functional.pic' as func

# constants
var w = 30
var h = 10
var newline = '
'

# pure functional part

const boardToStr = fn b -> {
    var rowStrs = for y in 0..h {
        var row = for x in 0..w {
            if b[x][y] == 1
                '# '
            else
                '. '
        }
        func.reduce(fn prev, new -> prev + new, '')(row)
    }
    func.reduce(fn prev, new -> prev + new + newline, '')(rowStrs)
}

const getCell = fn b, x, y ->
    if x >= 0 and x < w and y >= 0 and y < h
        b[x][y]
    else
        0

const getNeighbourCount = fn b, x, y ->
    getCell(b, x - 1, y - 1) +
    getCell(b, x, y - 1) +
    getCell(b, x + 1, y - 1) +
    getCell(b, x + 1, y) +
    getCell(b, x + 1, y + 1) +
    getCell(b, x, y + 1) +
    getCell(b, x - 1, y + 1) +
    getCell(b, x - 1, y)

const getNextState = fn b, x, y -> {
    var neighbours = getNeighbourCount(b, x, y)
    var alive = if getCell(b, x, y) == 1
                    neighbours == 2 or neighbours == 3
                else
                    neighbours == 3
    if alive 1 else 0
}

const getNextBoard = fn b ->
    for x in 0..w
        for y in 0..h
            getNextState(b, x, y)

# side effect part
var board = for x in 0..w for y in 0..h 0
board[3][1] = 1
board[3][2] = 1
board[3][3] = 1
board[2][3] = 1
board[1][2] = 1

board[23][1] = 1
board[21][2] = 1
board[20][3] = 1
board[20][4] = 1
board[20][5] = 1
board[21][5] = 1
board[22][5] = 1
board[23][5] = 1
board[24][5] = 1
board[25][4] = 1
board[25][2] = 1

while true {
    io.print(boardToStr(board))
    board = getNextBoard(board)
    time.sleep(0.25)
}